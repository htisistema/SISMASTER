*** CON211.PRG: Inclusao de Duplicatas (a pagar)
************************************************
MEMVAR getlist,nivel_acess,mdata_sis,cod_operado,memp_resa,;
       mend_firm,mcid_firm,muf_firm,mfone_firm,minsc_firm,mcgc_firm

FUNCTION con211(mcx)
****************
LOCAL MPRG:='CON211',;
      mtipo,mduplicata,mcod_ven,mtraco,opcao,tela_dupp,mobs,mvalor,mtip:=SPACE(1),;
      mextenso,mlinha,mlin,lin,lci,cci,lba,cba,mnum_banco,mc_c,mtp_pg:=' '

PRIVATE mcod_forn,mvenc,memissao,mnome_vend,mcom_ven,mfornece,mend,mbairro,mcidade,;
        muf,mcgc,mcpf,minsc,mbanco,mnum_nf,mcodconta,;
        mtipo_imp,mimp_tipo:=0,marq:=SPACE(35),mcodemp:=SPACE(3)

IF ! ver_nivel(mprg,'CONTA APAGAR (INCLUSAO DE DOCUMENTOS)','15',nivel_acess,,'AMBIE')
        RETURN NIL
ENDIF
lci := cci := 0
lba := 20
cba := 78
*------------------------------------------------------------------------------------
IF ! AbriArq('mastforn','forn');RETURN NIL;ENDIF
IF ! AbriArq('mastdupp','dupp');RETURN NIL;ENDIF
IF ! AbriArq('insopera','sen');RETURN NIL;ENDIF
IF ! AbriArq('mastcaixa','caix');RETURN NIL;ENDIF
*------------------------------------------------------------------------------------
mtraco := REPLI('-',80)
op_tela(05,10,29,90)
WHILE .T.
        limpa(00,00,30,100)
        exibi_prg(mprg)
        mcodemp := mcodempresa
        IF mcx = NIL
                Wvw_SetTitle( ,' CONTAS A PAGAR - Inclusao de Documentos ')           
                memissao:=mdata_sis
                mvenc   :=CTOD("  /  /  ")
                mtipo   :=SPACE(2)
                mbanco  := SPACE(1)
        ELSE
                Wvw_SetTitle( ,' EMISSAO DE DESPESAS ADMINISTRATIVA ')           
                memissao:=mdata_sis
                mvenc   :=mdata_sis
                mtipo   :='PA'
                mbanco  := 'C'
        ENDIF
*        DEVPOS(lci+1,cci+2);DEVOUT('Codigo da Empresa......:')
        DEVPOS(lci+2,cci+2);DEVOUT('Tipo Documento.........:     <DU>plicata <CH>eques <FR>ete <PA>gamentos')
        DEVPOS(lci+3,cci+2);DEVOUT('Numero Banco...........:')
        DEVPOS(lci+3,cci+32);DEVOUT('Conta:')
        DEVPOS(lci+4,cci+2);DEVOUT('Numero Documento.......:')
        DEVPOS(lci+5,cci+2);DEVOUT('Codigo do Fornecedor...:')
        DEVPOS(lci+6,cci+2);DEVOUT('Tipo de Conta..........:')
        DEVPOS(lci+7,cci+2);DEVOUT('Data de Emissao........:')
        DEVPOS(lci+8,cci+2);DEVOUT('Data de Vencimento.....:')
        DEVPOS(lci+9,cci+2);DEVOUT('Valor Duplicata R$.....:')
        DEVPOS(lci+10,cci+2);DEVOUT('Pag. [C]arteria [B]anco:')
        DEVPOS(lci+11,cci+2);DEVOUT('Tipo de Pagamento......:')
        DEVPOS(lci+12,cci+2);DEVOUT('Numero da Nota Fiscal..:')
        DEVPOS(lci+13,cci+2);DEVOUT('Observacao:')
        mtipo_imp := m_cfg[34]
        *****************
        SELE('dupp');ORDSETFOCUS(1)
        *****************
        mc_c := SPACE(10)
        mnum_banco := SPACE(3)
        mfornece := SPACE(40)
        mend := SPACE(35)
        mbairro := mcidade := SPACE(20)
        muf := SPACE(2)
        mcgc := minsc := SPACE(14)
        mcpf := SPACE(11)
        mcod_ven := mcom_ven := mcod_forn := mvalor := 0
        mnome_vend := SPACE(30)
        mduplicata=SPACE(10)
        mnum_nf := SPACE(6)
        mobs := SPACE(60)
        mcodconta := SPACE(4)
        mtp_pg  := ' '
        Mensagem('Digite o Numero da Documento. [ESC] Abandona.')
*       @ lci+1,cci+27 GET mcodemp PICT '999' VALID ver_emp(mcodemp,lci+1,cci+31) WHEN mmult_emp = 'S'
*       IF mcx = NIL
                @ lci+2,cci+27 GET mtipo PICT '@!' VALID mtipo $ 'CH,DU,FR,PA' WHEN mcx = NIL
*       ENDIF
        @ lci+3,cci+27 GET mnum_banco PICT '@!' WHEN mtipo = 'CH'
        @ lci+3,cci+40 GET mc_c PICT '@!' WHEN mtipo = 'CH'
        @ lci+4,cci+27 GET mduplicata PICT '@!' VALID IF(EMPTY(mduplicata),.F.,.T.)
        @ lci+5,cci+27 GET mcod_forn PICT "9999" VALID IF(EMPTY(mcod_forn),.F.,v_fornece(mcod_forn,lci+5,cci+32))
        READ
        IF LASTKEY()=27
                RESTSCREEN(00,00,24,79,tela_dupp)
                wvw_lclosewindow()
                RETURN NIL
        ENDIF
        *****************
        SELE('dupp');ORDSETFOCUS(1)
        GO TOP
        *****************
*       IF dupp->(DBSEEK(mtipo+mduplicata+' '+STRZERO(mcod_forn,4)+DTOS(mvenc)))
        IF dupp->(DBSEEK(mtipo+mduplicata+' '+STRZERO(mcod_forn,4)))
                atencao('Esta Documento ja existe e esta em ABERTO - Valor do Documento:'+TRANSFORM(dupp->valor,'999,999.99'))
                opcao := op_simnao('N','Deseja Incluir mesmo assim ? [S/n]:')
                IF LASTKEY() = 27 .OR. opcao = "N"
                        LOOP
                ENDIF
        ENDIF
        IF dupp->(DBSEEK(mtipo+mduplicata+'B'+STRZERO(mcod_forn,4)))
                atencao('Esta Documento ja existe e ja foi PAGO  - Valor do Documento:'+TRANSFORM(dupp->valor,'999,999.99'))
                opcao := op_simnao('N','Deseja Incluir mesmo assim ? [S/n]:')
                IF LASTKEY() = 27 .OR. opcao = "N"
                        LOOP
                ENDIF
        ENDIF
        Mensagem('Complete os Dados. [ESC] Abandona.')
        mfornece := forn->razao
        mcodconta := forn->tipo
        @ lci+5,cci+32 GET mfornece PICT "@!"
        @ lci+6,cci+27 GET mcodconta PICT '@@R 9.99.9' VALID IF(LEN(ALLTRIM(mcodconta))<4,.F.,verdesp(mcodconta,lci+6,cci+32)) .AND. lim_get() WHEN men_get(lci+7,cci+27,'Identifique o Grupo da Conta preenchendo so o primeiro digito da conta, ou preenchendo todo os espaco p/o tipo de conta <F1>Ajuda')
        //@ lci+6,cci+27 GET mcodconta PICT '@@R X.XX.X' VALID IF(LEN(ALLTRIM(mcodconta))<4,.F.,verdesp(mcodconta,lci+6,cci+32)) .AND. lim_get() WHEN men_get(lci+7,cci+27,'Identifique o Grupo da Conta preenchendo so o primeiro digito da conta, ou preenchendo todo os espaco p/o tipo de conta <F1>Ajuda')
        READ
        lim_get()
        IF LASTKEY() = 27;LOOP;lim_get();ENDIF
        setcor(3)
        DEVPOS(lci+6,cci+27);DEVOUT(mcodconta)
        setcor(1)
        verdesp(mcodconta,lci+6,cci+32)
        @ lci+7,cci+27 GET memissao WHEN mcx = NIL
        @ lci+8,cci+27 GET mvenc VALID vencim(mvenc) WHEN mcx = NIL
        @ lci+9,cci+27 GET mvalor PICT "999,999,999.99" VALID IF(ver_serie() = '975976',.T.,IF(mvalor<0,.F.,.T.))
        @ lci+10,cci+27 GET mbanco PICT "@!" VALID mbanco $ 'C,B, ' WHEN mcx = NIL
        @ lci+11,cci+27 GET mtp_pg PICT "@!" WHEN men_get(2,38,'Deixar em branco ou preencha com o tipo de pagamento que desajar')
        @ lci+12,cci+27 GET mnum_nf PICT "@!"
        @ lci+13,cci+14 GET mobs
        READ
        IF LASTKEY()=27
                LOOP
        ENDIF
        opcao := op_simnao('S','Confirma os Dados:')
        IF opcao = "N"
                LOOP
        ENDIF
        mtip := ' '
        **************
        SELE('dupp');ORDSETFOCUS(1)
        **************
        IF ADIREG()
                dupp-> tipo := mtipo
                dupp-> numero    := mnum_banco
                dupp-> c_c       := mc_c
                dupp-> duplicata := mduplicata
                dupp-> valor_dup := mvalor
                dupp-> tip_for := mcodconta
                dupp-> fornec := STRZERO(mcod_forn,4)
                dupp-> cliente := mfornece
                dupp-> emissao := memissao
                dupp-> venc := mvenc
                dupp-> valor := mvalor
                dupp-> pago := " "
                dupp-> banco := mbanco
                dupp-> tp_pg := mtp_pg
                dupp-> vendedor := cod_operado
                dupp-> num_ped := mnum_nf
                dupp-> obs      := mobs
                dupp-> operador := cod_operado
                IF memissao = mvenc
                        opcao := op_simnao('S','Deseja fazer a BAIXA deste documento agora:')
                        IF opcao = 'S'
                                dupp->datpag := mvenc
                                dupp->vlpago := mvalor
                                dupp->pago := "B"
                                dupp->banco := 'C'
                                mtip := 'S'
                        ENDIF
                ENDIF
        ELSE
                atencao('Nao foi possivel Acessar o Arquivo !!!')
                LOOP
        ENDIF
        DBUNLOCK()
        DBCOMMIT()
        IF mcx = NIL
                opcao := op_simnao('N','Deseja Imprimir o DOCUMENTO:')
                IF LASTKEY() = 27 .OR. opcao = 'N'
                        LOOP
                ENDIF
                SET DEVI TO PRINT
                SET PRINT TO (m_cfg[32])
                imprt(mtipo_imp,'E')
                pitalico(.T.)
                DEVPOS(PROW(),00);DEVOUT(memp_resa)
                pitalico(.F.)
                imprt(mtipo_imp,'N')
                DEVPOS(PROW()+1,00);DEVOUT(mend_firm)
                DEVPOS(PROW()+1,00);DEVOUT(mcid_firm)
                DEVPOS(PROW()+1,00);DEVOUT('Fone: '+mfone_firm)
                DEVPOS(PROW()+1,00);DEVOUT('Inscr. '+minsc_firm+'   C.G.C.: '+mcgc_firm)
                @ PROW()+1,00 SAY mtraco
                @ PROW()+1,00 SAY ' '
                @ PROW(),00 SAY 'Despesa:'
                @ PROW(),PCOL()+1 SAY STRZERO(mcod_forn,4)
                @ PROW(),PCOL()+1 SAY mfornece
                @ PROW()+1,00 SAY 'End.: '+mend
                @ PROW()+1,00 SAY 'Bairro: '+RTRIM(mbairro)+'-Cidade: '+RTRIM(mcidade)+'-'+muf
                IF mcgc <> SPACE(14)
                @ PROW()+1,00 SAY 'CNPJ:'
                        DEVPOS(PROW(),PCOL()+1);DEVOUTPICT(mcgc,'@@R 99.999.999/9999-99')
                        DEVPOS(PROW(),PCOL()+1);DEVOUT(' - Insc.: '+minsc)
                ELSEIF mcpf <> SPACE(11)
                        @ PROW()+1,00 SAY 'CPF:'
                        DEVPOS(PROW(),PCOL()+1);DEVOUTPICT(mcpf,'@@R 999.999.999-99')
                ENDIF
                @ PROW()+1,00 SAY mtraco
                imprt(mtipo_imp,'E',1)
                pitalico(.T.)
                @ PROW(),00 SAY 'DUPLICATA A PAGAR No.: '+mtipo+mduplicata
                pitalico(.F.)
                imprt(mtipo_imp,'N')
                @ PROW()+1,00 SAY mtraco
                @ PROW()+1,00 SAY 'Emissao:'
                @ PROW(),09 SAY memissao
                @ PROW(),38 SAY 'Vencimento:'
                @ PROW(),50 SAY mvenc
                @ PROW(),50 SAY mvenc
                @ PROW(),PCOL()+2 SAY 'Cod.:'
                @ PROW(),PCOL()+1 SAY cod_operado
                @ PROW()+1,00 SAY mtraco
                DEVPOS(PROW()+1,00);DEVOUT(PADC('Valor R$: ('+LTRIM(TRANSFORM(mvalor,'99,999,999.99'))+')',60))
                mextenso:= '('+extenso(mvalor,.T.,'real','reais')+')'
*               mextenso:= '('+RTRIM(extenso(mvalor,10))+')'
                mlinha := MLCOUNT(mextenso,80)
                FOR lin = 1 TO mlinha
                        mlin := MEMOLINE(mextenso,80,lin,,10)
                        DEVPOS(PROW()+1,00);DEVOUT(PADR(RTRIM(mlin),80))
                NEXT
                @ PROW()+1,00 SAY mtraco
                DEVPOS(PROW()+1,00);DEVOUT('Obs.: '+mobs)
                @ PROW()+1,00 SAY mtraco
                DEVPOS(PROW()+3,00);DEVOUT(PADC('Em ......../......../........',80))
                DEVPOS(PROW()+1,00);DEVOUT(PADC('Data do Aceite',80))
                DEVPOS(PROW()+2,00);DEVOUT(PADC('.................................           ...............................',80))
                DEVPOS(PROW()+1,00);DEVOUT(PADC('     Assinatura do Emitente                      Assinatura do Sacado',80))
                @ PROW()+1,00 SAY mtraco
                EJECT
                SETPRC(00,00)
                SET PRINT TO
                SET DEVI TO SCREEN
        ELSE
                ********************
                SELE('caix');ORDSETFOCUS(1)
                ********************
                IF ADIREG()
                        caix-> tipo := 'DA'
                        caix-> nota := ' DESPESA'
                        caix-> data := memissao
                        caix-> venci := mvenc
                        caix-> documento := mduplicata
                        caix-> valor := mvalor * -1
                        caix-> coddesp := SUBSTR(mcodconta,1,1)+SUBSTR(mcodconta,3)
                        caix-> descri1 := STRZERO(mcod_forn,4)+'-'+mfornece
                        caix-> descri2 := mobs
                        caix-> cod_opera := cod_operado
                        caix-> hora      := TIME()
                        COMMIT
                        UNLOCK
                ELSE
                        atencao('Nao foi possivel acessar o Arquivo !!!')
                        LOOP
                ENDIF
                opcao := op_simnao('N','Deseja Imprimir [S/n]:')
                IF LASTKEY() = 27 .OR. opcao = 'N'
                        LOOP
                ENDIF
                IF m_set[1,63] = 'F'
                        FOR i = 1 TO m_set[1,64]
                                SET DEVI TO PRINT
                                SET PRINTER TO (m_cfg[32])
*                               SET PRINTER TO DEPESA.REL
                                imprt(mtipo_imp,'E',2)
                                DEVPOS(PROW(),01);DEVOUT(memp_resa)
                                imprt(mtipo_imp,'N')
                                DEVPOS(PROW()+1,00);DEVOUT(mend_firm)
                                DEVPOS(PROW()+1,00);DEVOUT(mcid_firm +' - '+ mfone_firm)
                                DEVPOS(PROW()+1,00);DEVOUT('Inscr. '+minsc_firm+' -  C.G.C.: '+mcgc_firm)
                                DEVPOS(PROW()+1,00);DEVOUT(mtraco)

                                DEVPOS(PROW()+2,25);DEVOUT('D E S P E S A S')

                                DEVPOS(PROW()+1,00);DEVOUT(mtraco)
                                DEVPOS(PROW()+2,00);DEVOUT('Data da Despesa:')
                                DEVPOS(PROW(),PCOL()+1);DEVOUT(memissao)

                                DEVPOS(PROW()+2,00);DEVOUT('Valor Despesa..:')
                                DEVPOS(PROW(),PCOL()+1);DEVOUT(LTRIM(TRANSFORM(mvalor,'99,999,999.99')))

                                DEVPOS(PROW()+2,00);DEVOUT('Descricao......:')
                                DEVPOS(PROW(),17);DEVOUT(STRZERO(mcod_forn,4)+'-'+mfornece)
                                DEVPOS(PROW()+2,00);DEVOUT('Tipo de Conta..: '+mcodconta+' -')
                                verdesp(mcodconta,PROW(),PCOL()+1)
                                DEVPOS(PROW()+1,00);DEVOUT('Obs.: '+mobs)

                                DEVPOS(PROW()+4,00);DEVOUT(PADC('...........................           ............................',80))
                                DEVPOS(PROW()+1,00);DEVOUT(PADC('         C A I X A                              Solicitante       ',80))

                                DEVPOS(PROW()+2,00);DEVOUT(mtraco)
                                IF m_set[1,66] > 1 .AND. i < m_set[1,66]
                                        DEVPOS(PROW()+3,00);DEVOUT(' ')
                                ENDIF
                        NEXT
                        EJECT
                        SETPRC(00,00)
                        SET DEVI TO SCREEN;SET PRINT TO;SET PRINT OFF
                ELSEIF m_set[1,63] = 'B'
                        mtraco := REPLI('=',60)
                        FOR i = 1 TO m_set[1,64]
                                SET DEVI TO PRINT
                                SET PRINTER TO (m_cfg[32])
                                imprt(mtipo_imp,'N')
                                DEVPOS(PROW(),01);DEVOUT(memp_resa)
                                imprt(mtipo_imp,'C')
                                DEVPOS(PROW()+1,00);DEVOUT(mend_firm)
                                DEVPOS(PROW()+1,00);DEVOUT(mcid_firm +' - '+ mfone_firm)
                                DEVPOS(PROW()+1,00);DEVOUT('Inscr. '+minsc_firm+' -  C.G.C.: '+mcgc_firm)
                                DEVPOS(PROW()+1,00);DEVOUT(mtraco)

                                imprt(mtipo_imp,'N')
                                DEVPOS(PROW()+2,00);DEVOUT(PADC('D E S P E S A S',35))
                                imprt(mtipo_imp,'C')
                                DEVPOS(PROW()+1,00);DEVOUT(mtraco)

                                DEVPOS(PROW()+2,00);DEVOUT('Data da Despesa:')
                                DEVPOS(PROW(),PCOL()+1);DEVOUT(memissao)

                                DEVPOS(PROW()+2,00);DEVOUT('Valor Despesa..:')
                                DEVPOS(PROW(),17);DEVOUT(LTRIM(TRANSFORM(mvalor,'99,999,999.99')))

                                DEVPOS(PROW()+2,00);DEVOUT('Descricao......:')
                                DEVPOS(PROW(),17);DEVOUT(STRZERO(mcod_forn,4)+'-'+mfornece)
                                DEVPOS(PROW()+1,00);DEVOUT('Obs.: '+mobs)

                                DEVPOS(PROW()+3,00);DEVOUT(PADC('...........................',60))
                                DEVPOS(PROW()+1,00);DEVOUT(PADC('         C A I X A         ',60))
                                DEVPOS(PROW()+3,00);DEVOUT(PADC('...........................',60))
                                DEVPOS(PROW()+1,00);DEVOUT(PADC('         Solicitante       ',60))

                                DEVPOS(PROW()+2,00);DEVOUT(mtraco)
                                IF m_set[1,66] > 1 .AND. i < m_set[1,66]
                                        DEVPOS(PROW()+3,00);DEVOUT(' ')
                                ENDIF
                        NEXT
                        DEVPOS(PROW()+m_cfg[29],00);DEVOUT(' ')
                        SETPRC(00,00)
                        SET DEVI TO SCREEN;SET PRINT TO;SET PRINT OFF
                ENDIF
        ENDIF
ENDDO
wvw_lclosewindow()
RETURN NIL
*********************** f i m *************************************
